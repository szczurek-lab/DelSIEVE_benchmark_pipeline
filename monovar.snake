"""
Explanation of wildcards:

dataType: input data types, e.g., no CNVs, including CNVs, excluding CNVs, etc.
datasetName: simulated dataset names, e.g., 0001, 0002, 0003...
"""


include: "scripts/constants.py"
include: "scripts/utils.py"


#########################################################
#              Get categories of wildcards              #
#########################################################

SIMUDATASETNAMES = get_dataset_names(
    SIMUSNVSITESDIR,
    config["fineTune"]["indelsieve_simulator"]["subsample"],
)
SIMUDATATYPES = get_data_types(
    config["benchmark"]["simulatedDataTypes"],
    config["benchmark"]["simulation"]["dataTypes"]["CNVTypes"],
)


#######################################################
#                MONOVAR - True params                #
#######################################################

# Rule: get a list of dummy bam file names
rule getBamFileNamesMonovar:
    input:
        bamDir=SIMUDUMMYBAMDATADIR,
        tumCellNames=SIMUTUMORCELLNAMES
    output:
        protected(f'{MONOVARDIR}{config["benchmark"]["bamFileNamesWithoutNormal"]}.{{datasetName}}'),
    params:
        tumorCellPre=config["benchmark"]["simulation"]["tumorCellDataPre"]
    shell:
        "python scripts/get_bam_file_names.py "
        "--ibam {input.bamDir} "
        "--icell {input.tumCellNames} "
        "--type 1 "
        "--prefix {params.tumorCellPre} "
        "-o {output}"


def getMPileupWithoutNormal(wildcards, input) -> str:
    ret: str = ""
    for dir_path, _, file_names in os.walk(input.mpileupDir):
        files: list[str] = [
            i
            for i in file_names
            if i == "merged.pileup"
        ]

        assert len(files) == 1
        ret = os.path.join(
            dir_path,
            files[0],
        )
        break

    return ret


# Rule: run MOVONAR with TRUE parameters
rule runMonovarWithTrueParams:
    input:
        # flagFile=SIMUDATATYPESDIR + "DONE_FLAG",
        mpileupDir=SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["MPileUpDataDir"],
        refFile=SIMURAWDATADIR + "{datasetName}/" + config["benchmark"]["simulation"]["refGenomeDataPre"] + config["benchmark"]["simulation"]["refGenomeDataSuf"],
        bamFileNames=f'{MONOVARDIR}{config["benchmark"]["bamFileNamesWithoutNormal"]}.{{datasetName}}'
    output:
        protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/monovar.vcf")
    params:
        monovar=config["tools"]["monovar"],
        mpileupFile=lambda wildcards, input: getMPileupWithoutNormal(
            wildcards,
            input,
        ),
        fpPrior=config["benchmark"]["analysis"]["effSeqErrRate"],
        fnPrior=config["benchmark"]["analysis"]["adoRate"]
    threads:
        config["fineTune"]["monovar"]["threads"]
    conda:
        "scripts/envs/monovar.yaml"
    benchmark:
        repeat(EFFICIENCYMONOVARDIR + "{dataType}/{datasetName}.tsv", config["benchmark"]["efficiency"]["repetition"])
    shell:
        "cat {params.mpileupFile} | "
        "{params.monovar} "
        "-p {params.fpPrior} "
        "-a {params.fnPrior} "
        "-t 0.05 "
        "-m {threads} "
        "-f {input.refFile} "
        "-b {input.bamFileNames} "
        "-o {output}"

# Rule: filter VCF results from MONOVAR with TRUE parameters
rule filterMonovarVarInfoTrueParams:
    input:
        MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/monovar.vcf"
    output:
        gz=protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/monovar.vcf.gz"),
        filtered=protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/filtered.vcf")
    shell:
        "bgzip -c {input} > {output.gz} && "
        "tabix -p vcf {output.gz} && "
        "bcftools filter "
        "--include \"FILTER='PASS'\" "
        "--output-type v "
        "--output {output.filtered} "
        "{output.gz}"

# Rule: get variant calling information from MONOVAR with TRUE parameters
rule getMonovarVarInfoTrueParams:
    input:
        MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/filtered.vcf"
    output:
        cellNames=protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.cell_names"),
        lociInfo=protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.loci_info"),
        genotypes=protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.genotypes"),
        ternary=protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.ternary"),
        genotypeProbs=protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.genotype_probs"),
        sifitData=protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.sifit_data"),
        sifitCellNames=protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.sifit_cell_names")
    params:
        prefix=MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered"
    shell:
        "python scripts/extract_info_from_vcf.py "
        "--mode monovar "
        "--prefix {params.prefix} "
        "--sifit t "
        "{input}"


#######################################################
#       Gather the number of sites from MONOVAR       #
#######################################################

rule gatherSitesInfoMonovar:
    input:
        trueSites=expand(SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["trueSNVSitesNamesPre"], dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        candidateSites=expand(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.loci_info", dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES)
    output:
        protected(ANALYSISSITESINFODIR + "from_monovar_" + config["benchmark"]["analysis"]["trueParameters"] + ".tsv")
    params:
        datasetNames=SIMUDATASETNAMES,
        tool="monovar",
        snvType=None,
        toolSetup=config["benchmark"]["analysis"]["trueParameters"],
        fineTuneType=None,
        cnvRate=str(config["fineTune"]["simulateCNVs"]["prob"]),
        dataType=SIMUDATATYPES,
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        mutationRate=config["benchmark"]["analysis"]["mutationRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"],
        doubletRate=config["benchmark"]["analysis"]["doubletRate"],
        typeOfQuery=config["benchmark"]["indelsieve"]["typeOfQuery"]
    script:
        "scripts/gather_sites_info_part.py"


########################################################
#       Get variant calling results from MONOVAR       #
########################################################

# Rule: gather filtered variant calling results from MONOVAR working with TRUE parameters
rule gatherMonovarVarResultsTrueParams:
    input:
        cellNames=expand(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.cell_names", dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        lociInfo=expand(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.loci_info", dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        genotypes=expand(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.genotypes", dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        ternary=expand(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.ternary", dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        genotypeProbs=expand(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/post_processed/filtered.genotype_probs", dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        trueCellNames=expand(f'{SIMUTUMORCELLNAMESDIR}{config["benchmark"]["simulation"]["tumorCellNamesPre"]}.{{datasetName}}', datasetName=SIMUDATASETNAMES),
        trueSNVSites=expand(SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["trueSNVSitesNamesPre"], dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        trueSNVGenotypesNU=expand(SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["trueSNVGenotypesNUPre"], dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES),
        trueSNVGenotypesTer=expand(SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["trueSNVGenotypesTerPre"], dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES)
    output:
        protected(ANALYSISVARCALLDIR + "from_monovar_" + config["benchmark"]["analysis"]["trueParameters"] + ".tsv")
    params:
        forSieve=False,
        datasetNames=SIMUDATASETNAMES,
        tool="monovar",
        snvType=None,
        toolSetup=config["benchmark"]["analysis"]["trueParameters"],
        fineTuneType=None,
        cnvRate=str(config["fineTune"]["simulateCNVs"]["prob"]),
        dataType=SIMUDATATYPES,
        cellNum=config["benchmark"]["analysis"]["cellNum"],
        covMean=config["benchmark"]["analysis"]["covMean"],
        covVariance=config["benchmark"]["analysis"]["covVariance"],
        effSeqErrRate=config["benchmark"]["analysis"]["effSeqErrRate"],
        adoRate=config["benchmark"]["analysis"]["adoRate"],
        mutationRate=config["benchmark"]["analysis"]["mutationRate"],
        deletionRate=config["benchmark"]["analysis"]["deletionRate"],
        insertionRate=config["benchmark"]["analysis"]["insertionRate"],
        doubletRate=config["benchmark"]["analysis"]["doubletRate"]
    script:
        "scripts/gather_variant_calling_results_part.py"


# # Function: get the FASTA file of the reference genome.
# def get_fasta_file(wildcards):
#     if wildcards.dataType in SIMUNOCNVTYPES:
#         return f'{SIMUDATATYPESDIR}{{dataType}}/{{datasetName}}/{config["benchmark"]["simulation"]["dataTypes"]["mappedReadsDir"]}{config["benchmark"]["simulation"]["refGenomeDataPre"]}{config["benchmark"]["simulation"]["refGenomeDataSuf"]}'
#     elif wildcards.dataType in SIMUNOCNVTYPES:
#         folder = checkpoints.addCNVs2SimulatedData.get(
#             CNVType=wildcards.dataType,
#             datasetName=wildcards.datasetName
#         ).output[0]
#         return os.path.join(folder, f'{config["benchmark"]["simulation"]["refGenomeDataPre"]}{config["benchmark"]["simulation"]["refGenomeDataSuf"]}')
#     else:
#         raise ValueError(f'Unknown dataType: {wildcards.dataType}.')

# # Rule: run MOVONAR with TRUE parameters
# rule runMonovarTest:
#     input:
#         bam=expand(SIMUDATATYPESDIR + "{{dataType}}/{{datasetName}}/" + config["benchmark"]["simulation"]["dataTypes"]["sortedBamDir"] + "{cellName}.bam", cellName=CELLNAMES),
#         fa=lambda wildcards: get_fasta_file(wildcards),
#         bamIdx=expand(SIMUDATATYPESDIR + "{{dataType}}/{{datasetName}}/" + config["benchmark"]["simulation"]["dataTypes"]["sortedBamDir"] + "{cellName}.bam.bai", cellName=CELLNAMES),
#         faIdx=SIMUDATATYPESDIR + "{dataType}/{datasetName}/" + config["benchmark"]["simulation"]["dataTypes"]["mappedReadsDir"] + config["benchmark"]["simulation"]["refGenomeDataPre"] + config["benchmark"]["simulation"]["refGenomeDataSuf"] + ".fai",
#         bamFileNames=f'{MONOVARDIR}{config["benchmark"]["bamFileNamesWithoutNormal"]}.{{datasetName}}'
#     output:
#         protected(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/monovar_test.vcf")
#     params:
#         monovar=config["tools"]["monovar"],
#         fpPrior=config["benchmark"]["analysis"]["effSeqErrRate"],
#         fnPrior=config["benchmark"]["analysis"]["adoRate"]
#     threads:
#         config["fineTune"]["monovar"]["threads"]
#     conda:
#         "scripts/envs/monovar.yaml"
#     benchmark:
#         repeat(EFFICIENCYMONOVARDIR + "{dataType}/{datasetName}.tsv", config["benchmark"]["efficiency"]["repetition"])
#     shell:
#         "samtools mpileup -f {input.fa} {input.bam} | "
#         "{params.monovar} "
#         "-p {params.fpPrior} "
#         "-a {params.fnPrior} "
#         "-t 0.05 "
#         "-m {threads} "
#         "-f {input.fa} "
#         "-b {input.bamFileNames} "
#         "-o {output}"

# rule testMonovar:
#     input:
#         expand(MONOVARTRUEPARAMSDIR + "{dataType}/{datasetName}/monovar_test.vcf", dataType=SIMUDATATYPES, datasetName=SIMUDATASETNAMES)
#     output:
#         protected(MONOVARTRUEPARAMSDIR + "TEST_DONE")
#     shell:
#         "touch {output}"

